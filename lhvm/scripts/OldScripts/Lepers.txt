///////////////////////////////////////////////////////////////////////////////////
//	Lepers Event - Land 2
//	~~~~~~~~~~~~~~~~~~~~~
//
//	A group of Aztec lepers approach the land mass by ship.
//	Aztec lepers depart the vessel, and slowly make their way towards your town.
//	Once arrived, they ask to enter and join your town.
//	If rejected, they flee towards the opposing base; some are killed by the creature
//	on the island; and those that make it to the enemy base are then slaughtered.
//	If welcomed in, they join the population but this has a negative impact on 
//  the lifespan levels of your villagers.
//
//	Rewards:			Population boost.
//	Drawbacks:			Lowering of lifespan of your villagers.
//
//	Start date:			06-03-03			By:	Phil
//	Last Update:		19-03-03			By: Phil
//
//	Requires:	Boat object, Floating anim?, Docking ship anim, 
//				Leper version of Aztec men/women.
//				Max age (Lifespan) of villagers in a town needs to be changeable.
//
//	Work Needed:	More checking of exists and error messaging.
//					Currently written on Land 3 not 2! New positions/waypoints...
//					Fleeing anim and/or screams from the lepers whilst fleeing.
//					The Lepers, once fleeing, need to be slaughtered by opposition.
//
//	Conversion:		All markers converted to new world.
//					Develop ship element a little further + more timer stuff.
//
///////////////////////////////////////////////////////////////////////////////////


//-----------------------------------------------------------------------------
//	Constants
//-----------------------------------------------------------------------------
define L_FALSE					= 0
define L_TRUE					= 1
define L_LEPER_GROUP_SIZE		= 15
define L_WAYPOINT_RADIUS		= 20


//-----------------------------------------------------------------------------
//	Globals
//-----------------------------------------------------------------------------
global L_Waypoints[4]				//... Waypoint array
global L_Leper[15]					//... Leper array
global L_Fleeing_Lepers_A[8]		//... Fleeing leper group A
global L_Fleeing_Lepers_B[8]		//... Fleeing leper group B
global L_Ship_Reached_Point_A		= L_FALSE
global L_Lepers_At_Creature			= L_FALSE
global L_Lepers_At_Opposing_Town	= L_FALSE
global L_Lepers_Released_Into_Town	= L_FALSE
global L_Got_Leper_In_Hand			= L_FALSE
global L_Leader_In_Hand				= L_FALSE
global L_Stopped					= L_FALSE
global L_Leper_Picked_Up			= L_FALSE
global L_Lepers_Accepted			= L_TRUE
global L_Leper_Leader				= 0
global L_Leper_Ship					= 0
global L_Leper_Flock				= 0
global L_Leper_Counter				= 0
global L_Players_Town				= 0
global L_Norse_Creature_Position	= 0
global L_Norse_Town_Outskirts		= 0
global L_Dead_Leper_Counter			= 0
global L_Counter_G					= 0
global L_Counter_M					= 0
global L_Counter_L					= 0
global L_Held_Object				= 0
global L_Fleeing_Lepers_Group_A		= 0
global L_Fleeing_Lepers_Group_B		= 0
global L_Boat_Timer					= 0



//-----------------------------------------------------------------------------
//	Script defines
//-----------------------------------------------------------------------------
define script L_Good_Spirit_Spots_Ship
define script L_Leper_Ship_Appears
define script L_Spirits_Argue_Over_Ship
define script L_Create_Leper_Flock
define script L_Spirits_Inform_Player_Of_Ship_Arrival
define script L_Lepers_Accepted_In
define script L_Spirits_Discuss_Lepers
define script L_Are_Lepers_Within_Radius_1
define script L_Are_Lepers_Within_Radius_2
define script L_Evil_Spirit_Explains_Impact_On_Age
define script L_Have_Fleeing_Lepers_Reached_Opposing_Town
define script L_Have_Fleeing_Lepers_Reached_Creature
define script L_Spirits_Show_Leper_Deaths
define script L_Lepers_React_To_God_Hand
define script L_Are_The_Lepers_Being_Hurt
define script L_Move_Lepers_Via_Waypoints
define script L_Check_If_Leper_Picked_Up
define script L_Lepers_Watch_The_Hand
define script L_Check_For_Any_Dead_Lepers
define script L_Create_Leper_Ship


//... Run the main script not required.
//run script Lepers



//========================================================================================
//	Start of MAIN script
//========================================================================================

//=======================
begin script Lepers

start

	L_Players_Town				= get town with id 0
	L_Norse_Creature_Position	= marker at {1202.335,3.350,1435.501}
	L_Norse_Town_Outskirts		= marker at {1359.728,28.792,1467.305}

	run script L_Good_Spirit_Spots_Ship
	run script L_Leper_Ship_Appears
	run script L_Spirits_Argue_Over_Ship
	run script L_Create_Leper_Ship
	run script L_Spirits_Inform_Player_Of_Ship_Arrival
	run script L_Create_Leper_Flock

    run background script L_Are_Lepers_Within_Radius_1
	run background script L_Are_Lepers_Within_Radius_2
	run background script L_Check_If_Leper_Picked_Up
	run background script L_Are_The_Lepers_Being_Hurt
	run background script L_Lepers_Watch_The_Hand
	run background script L_Check_For_Any_Dead_Lepers
	run background script L_Lepers_Accepted_In
	run script L_Move_Lepers_Via_Waypoints
		
    //... Have the lepers reached the final waypoint without the player killing any?
	wait until L_Counter_M == 4 or L_Lepers_Accepted == L_FALSE

	if L_Lepers_Accepted == L_TRUE
		run script L_Evil_Spirit_Explains_Impact_On_Age
	elsif L_Lepers_Accepted == L_FALSE
		run background script L_Have_Fleeing_Lepers_Reached_Creature
		run background script L_Have_Fleeing_Lepers_Reached_Opposing_Town
		run script L_Spirits_Show_Leper_Deaths
	end if

	wait 100 seconds

end script Lepers
//=======================



//-----------------------------------------------------------------------------
//	L_Good_Spirit_Spots_Ship
//-----------------------------------------------------------------------------
begin script L_Good_Spirit_Spots_Ship

	L_Speech_Counter			= 0

start

	L_Boat_Timer = create timer for 120 seconds
	//set L_Boat_Timer time to 20 seconds
	//or get L_Boat_Timer time remaining <= 0

	begin loop
		begin dialogue
			eject good spirit
			if L_Speech_Counter == 0
				say "There's a ship on the horizon to the West."
			end if
			if L_Speech_Counter == 1
				say "The ship to the west seems to be approaching our land."
			end if
			wait until read
			clear dialogue
		end dialogue
		L_Speech_Counter++
		wait 30 seconds
		until L_Ship_Reached_Point_A == L_TRUE or get L_Boat_Timer time remaining <= 60
	end loop

end script L_Good_Spirit_Spots_Ship



//-----------------------------------------------------------------------------
//	L_Leper_Ship_Appears
//-----------------------------------------------------------------------------
begin script L_Leper_Ship_Appears

	L_Ship_Start_Position		= marker at {796.914,0.000,1569.139}		//requires Land 2 position
	L_Ship_Destination			= marker at {994.237,0.000,1477.506}
		
start

	//... Zoom in a little towards the ship docking area of the map.
	//begin cinema
	//	move camera position to {1055.089,35.044,1431.516} time 5
	//	move camera focus to {1006.742,0.789,1464.521} time 5
	//end cinema

	//... wait until ship's start position isn't visible
	//wait until {L_Ship_Start_Position} not viewed
	
	//... create leper's ship
	//L_Leper_Ship = create OBJECT MOBILE_OBJECT_INFO_ARK at {L_Ship_Start_Position}
	//move L_Leper_Ship position to {L_Ship_Destination}
	
end script L_Leper_Ship_Appears



//-----------------------------------------------------------------------------
//	L_Spirits_Argue_Over_Ship
//-----------------------------------------------------------------------------
begin script L_Spirits_Argue_Over_Ship

start

	begin dialogue
		eject evil spirit
    	say "That's an Aztec ship. I say blow it out of the water before they can land."
		wait until read
		clear dialogue
		make evil spirit disappear
		eject good spirit
		say "They don't seem to have brought weapons with them, they might be friendly."
		wait until read
		clear dialogue
		send good spirit home
	end dialogue

end script L_Spirits_Argue_Over_Ship



//-----------------------------------------------------------------------------
//	L_Create_Leper_Ship
//-----------------------------------------------------------------------------
begin script L_Create_Leper_Ship

	L_Temp_Boat_Abode_Position	= marker at {991.750,0.356,1471.937}

start

	wait until get L_Boat_Timer time remaining <= 1

	//... TEMPORARILY USING A HOUSE AS A SHIP
	//wait until {L_Temp_Boat_Abode_Position} not viewed
	L_Leper_Ship = create with angle 180 and scale 1.0 HOUSE ABODE_INFO_GREEK_SHACK_X at {L_Temp_Boat_Abode_Position}

	//... ensure "Leper ship" has been created ok
	begin dialogue
		if L_Leper_Ship not exists
			say "Leper_Script_Error - Leper Ship doesn't exist"
			wait until read
		end if
		clear dialogue
	end dialogue

end script L_Create_Leper_Ship

	

//-----------------------------------------------------------------------------
//	L_Spirits_Inform_Player_Of_Ship_Arrival
//-----------------------------------------------------------------------------
begin script L_Spirits_Inform_Player_Of_Ship_Arrival

start

	begin dialogue
		eject good spirit
		say "That Aztec ship has docked. Let's see who emerges."
		wait until read
		clear dialogue
		send good spirit home
	end dialogue
	
end script L_Spirits_Inform_Player_Of_Ship_Arrival



//-----------------------------------------------------------------------------
//	L_Create_Leper_Flock
//-----------------------------------------------------------------------------
begin script L_Create_Leper_Flock
	
	L_Leper_Leader_Spawn_Position	= marker at {991.763,0.387,1471.807}
	L_Leader_Flocking_Position		= marker at {991.156,2.023,1467.966}
	L_Counter_A						= 0
	L_Delay							= 2
	L_Temp_Var						= 0
	L_Delay_B						= 0
	L_Said_Already					= 0

start

	L_Temp_Var = number from 4000 to 9000
	//... Integers only
	L_Delay_B = L_Temp_Var / 10000 

	//... create Leper Leader
	L_Leper_Leader = create VILLAGER VILLAGER_INFO_AZTEC_SHEPHERD_MALE at {L_Leper_Leader_Spawn_Position}
	
	//... Make Leper leader emerge from the boat
	move L_Leper_Leader position to {L_Leader_Flocking_Position}

	//... Wait until the leader reaches the waypoint
	wait until {L_Leper_Leader} near {L_Leader_Flocking_Position} radius 2
	
	//... Wait until L_Leper_Leader ready
	wait L_Delay seconds
	
	//... Turn him to face the other lepers as they saunter off the ship.
	set L_Leper_Leader focus to {L_Leper_Ship}
	
	//... define flock properties
	L_Leper_Flock = flock at {L_Leader_Flocking_Position}
	set L_Leper_Flock properties inner 5 outer 15
	
	//... Create all lepers, place them into a flock
	while L_Counter_A < L_LEPER_GROUP_SIZE
		L_Leper[L_Counter_A] = create VILLAGER VILLAGER_INFO_AZTEC_SHEPHERD_MALE at {L_Leper_Leader_Spawn_Position}
		//SPEED of L_Leper[L_Counter_A] = 2  //... TEMPORARY SPEED BOOST
		HEALTH of L_Leper[L_Counter_A] = 1
		enable L_Leper[L_Counter_A] poisoned
		move L_Leper[L_Counter_A] position to {L_Leader_Flocking_Position} + {number from -5 to 5, number from 1 to 7}
		wait L_Delay_B seconds
		attach L_Leper[L_Counter_A] to L_Leper_Flock
		L_Leper_Counter++
		L_Counter_A++
		if L_Counter_A == 4 and L_Said_Already == 0
			run background script L_Spirits_Discuss_Lepers //... Run background Spirits dialogue
			L_Said_Already = 1
		end if
	end while

	wait 1 seconds
		
	//... Make the Leper Leader the leader of the flock
	attach L_Leper_Leader to L_Leper_Flock as leader
	wait 1 seconds
	L_Leper_Counter++

	state L_Leper_Flock LIVING_MOVE_IN_FLOCK

	wait 1 seconds
		
end script L_Create_Leper_Flock



//-----------------------------------------------------------------------------
//	L_Spirits_Discuss_Lepers
//-----------------------------------------------------------------------------
begin script L_Spirits_Discuss_Lepers

start

	begin dialogue
		eject evil spirit
		say "Urgh! Those people coming out of the ship are lepers or something!"
		wait until read
		clear dialogue
		make evil spirit disappear
		eject good spirit
		say "Hmm. Maybe we can heal them. Lets see what they do at least!"
		wait until read
		clear dialogue
		send good spirit home
	end dialogue

end script L_Spirits_Discuss_Lepers



//-----------------------------------------------------------------------------
//	L_Are_Lepers_Within_Radius_1
//-----------------------------------------------------------------------------
begin script L_Are_Lepers_Within_Radius_1

start

	//... Wait until the lepers have completed about 70% of their journey towards the player's base.
	//wait until {L_Leper_Flock} near {L_Waypoint_C} radius L_WAYPOINT_RADIUS
	wait until {L_Leper_Flock} near {L_Waypoints[1]} radius L_WAYPOINT_RADIUS

	begin dialogue
		eject evil spirit
		say "Those lepers are closing in! Kill em now Boss!"
		wait until read
		clear dialogue
		make evil spirit disappear
		eject good spirit
		say "Why hurt them? They would give a boost to our population."
		wait until read
		clear dialogue
		send good spirit home
	end dialogue

end script L_Are_Lepers_Within_Radius_1



//-----------------------------------------------------------------------------
//	L_Are_Lepers_Within_Radius_2
//-----------------------------------------------------------------------------
begin script L_Are_Lepers_Within_Radius_2

start

	//... Wait until the Lepers have got fairly close to the player's town
	//wait until {L_Leper_Flock} near {L_Waypoint_D} radius 30 //L_WAYPOINT_RADIUS
	wait until {L_Leper_Flock} near {L_Waypoints[2]} radius L_WAYPOINT_RADIUS

	begin dialogue
		eject evil spirit
		say "Last chance to kill those lepers Boss; they're nearly at our town!"
		wait until read
		clear dialogue
		make evil spirit disappear
	end dialogue
	
end script L_Are_Lepers_Within_Radius_2



//-----------------------------------------------------------------------------
//	L_Lepers_Accepted_In
//-----------------------------------------------------------------------------
begin script L_Lepers_Accepted_In

	L_Accepted_Town_Dest		= marker at {997.026,2.680,1314.402}
	L_House_Position			= marker at {985.727,2.739,1334.445}
	L_Counter_D					= 0

start

	//... Wait until the Lepers have reached the edge of your town
	wait until {L_Leper_Flock} near {L_Waypoints[3]} radius L_WAYPOINT_RADIUS

	//... Attach the lepers to the player's town. 
	if L_Lepers_Accepted == L_TRUE
		
		move L_Leper_Flock position to {L_Accepted_Town_Dest}
		wait until {L_Leper_Flock} near {L_Accepted_Town_Dest} radius L_WAYPOINT_RADIUS
		while L_Counter_D < L_LEPER_GROUP_SIZE
			attach L_Leper[L_Counter_D] to L_Players_Town
			release L_Leper[L_Counter_D]
			L_Counter_D++
		end while

		wait 2 seconds

		L_Lepers_Released_Into_Town = L_TRUE

		begin dialogue // TEMP MSG
			say "Congrats - Lepers released into your village."
			wait until read
			clear dialogue
		end dialogue
	
	end if
	
end script L_Lepers_Accepted_In



//-----------------------------------------------------------------------------
//	L_Are_The_Lepers_Being_Hurt
//-----------------------------------------------------------------------------
begin script L_Are_The_Lepers_Being_Hurt

	L_Counter_H					= 0

start

	begin loop

	while L_Dead_Leper_Counter < 2
		if HEALTH of L_Leper[L_Counter_H] < 0.1 //0.2
			L_Dead_Leper_Counter++
		end if
		L_Counter_H++
		if L_Counter_H > 14
			L_Counter_H = 0
		end if
	end while
	
	wait 0.3 seconds

	end loop

end script L_Are_The_Lepers_Being_Hurt



//-----------------------------------------------------------------------------
//	L_Lepers_React_To_God_Hand
//-----------------------------------------------------------------------------
begin script L_Lepers_React_To_God_Hand

	L_Close_Up_Cutscene_Leper		= 0
	L_Picked_Up_Counter				= 0
	L_Counter_I						= 0

start

	//begin loop

		wait until L_Got_Leper_In_Hand == L_TRUE

		L_Picked_Up_Counter++

		
		if L_Leader_In_Hand == L_FALSE
			move L_Leper_Leader position to {L_Leper_Leader}
			set L_Leper_Leader focus to {L_Leper[L_Counter_G]} //{hand}
		end if

		//... a circle-round command would be nice here...
		while L_Counter_I < L_LEPER_GROUP_SIZE
			move L_Leper[L_Counter_I] position to {L_Leper[L_Counter_I]}
			set L_Leper[L_Counter_I] focus to {L_Leper[L_Counter_G]} //{hand}//{L_Leper_Leader}
			L_Counter_I++
		end while

		//... Reactions not working?
		//attach reaction hand REACTION_LOOK_AT_OBJECT
		//attach reaction hand REACTION_REACT_TO_VILLAGER_IN_HAND	

		if L_Leader_In_Hand == L_TRUE
			//... Choose random leper from group
			// = L_Close_Up_Cutscene_Leper
		else
			L_Leper_Leader = L_Close_Up_Cutscene_Leper
		end if

		//begin cinema
			//set camera to face L_Close_Up_Cutscene_Leper distance 15
			//set camera to face L_Leper_Leader distance 15
		//end cinema

		begin dialogue
			if L_Picked_Up_Counter == 1
				say "Please don't hurt us! We mean you no harm."
			end if
			if L_Picked_Up_Counter == 2
				say "Argh not again! Please put us down! We just want somewhere to live."
			end if
			if L_Picked_Up_Counter == 3
				say "This picking people up is becoming a bit of a habit. Please put us down!"
			end if
			wait until read
			clear dialogue
		end dialogue

		//wait until L_Got_Leper_In_Hand == L_FALSE

		//... Reactions not working?
		//detach reaction hand
	
		if L_Picked_Up_Counter > 2
			L_Picked_Up_Counter = 0
		end if 

		//wait 1 seconds

	//end loop


end script L_Lepers_React_To_God_Hand



//-----------------------------------------------------------------------------
//	L_Evil_Spirit_Explains_Impact_On_Age
//-----------------------------------------------------------------------------
begin script L_Evil_Spirit_Explains_Impact_On_Age

start

	wait until L_Lepers_Released_Into_Town == L_TRUE
	
	wait 20 seconds	
	//... SHOULD BE: Wait until lifespan effect is noticed.

	begin dialogue
		eject evil spirit
    	say "Hey Boss! Our people aren't living as long as they used to; it'll be those lepers I bet you."
		wait until read
		clear dialogue
		make evil spirit disappear
		eject good spirit
		say "They helped boost our population though didn't they!"
		wait until read
		clear dialogue
		send good spirit home
	end dialogue

end script L_Evil_Spirit_Explains_Impact_On_Age



//-----------------------------------------------------------------------------
//	L_Have_Fleeing_Lepers_Reached_Creature
//-----------------------------------------------------------------------------
begin script L_Have_Fleeing_Lepers_Reached_Creature

start


	//... Wait until the fleeing flock is within striking range of the Norse creature.
	wait until {L_Fleeing_Lepers_Group_A} near {L_Norse_Creature_Position} radius 30
	L_Lepers_At_Creature = L_TRUE


end script L_Have_Fleeing_Lepers_Reached_Creature



//-----------------------------------------------------------------------------
//	L_Have_Fleeing_Lepers_Reached_Opposing_Town
//-----------------------------------------------------------------------------
begin script L_Have_Fleeing_Lepers_Reached_Opposing_Town

start

	wait until {L_Fleeing_Lepers_Group_B} near {L_Norse_Town_Outskirts} radius 30
	L_Lepers_At_Opposing_Town = L_TRUE

end script L_Have_Fleeing_Lepers_Reached_Opposing_Town



//-----------------------------------------------------------------------------
//	L_Spirits_Show_Leper_Deaths
//-----------------------------------------------------------------------------
begin script L_Spirits_Show_Leper_Deaths

	L_Shown_Creature_Killing_Lepers			= L_FALSE
	L_Shown_Norse_Village_Killing_Lepers	= L_FALSE

start

	wait 8 seconds	

	begin dialogue
		eject evil spirit
    	say "Nice work Boss! Look at those lepers scurrying off. Hehe!"
		wait until read
		clear dialogue
		send evil spirit home
	end dialogue

	//... Wait until the fleeing lepers have reached either the creature or the opposing town.
	wait until L_Lepers_At_Creature == L_TRUE or L_Lepers_At_Opposing_Town == L_TRUE

	//... Show both the creature and the opposing village killing the lepers.
	begin loop
		
		//... If some of the lepers make it to the Norse creature a horrible fate awaits them.
		if L_Lepers_At_Creature == L_TRUE and L_Shown_Creature_Killing_Lepers == L_FALSE
			begin dialogue
				eject evil spirit
				say	"Haha look! Some of the lepers are trying to get past that huge creature."
				wait until read
				clear dialogue
				send evil spirit home
			end dialogue
			//... Norse creature grabs a leper and eats him.
			//... Norse creature grabs another and throws him out to sea.
			//... Treads on another?
			//... etc...
			L_Shown_Creature_Killing_Lepers = L_TRUE
		end if

		//... A worse fate? awaits the lepers that reach the Norse Village - burnt alive?
		if L_Lepers_At_Opposing_Town == L_TRUE and L_Shown_Norse_Village_Killing_Lepers == L_FALSE
			begin dialogue
				eject evil spirit
		 		say	"Hehe! Let's see how the Norse deal with these lepers."
				wait until read
				clear dialogue
				send evil spirit home
			end dialogue
			//... Norse soldiers slaughter the remaining lepers who have made it to the Norse village.
			//... Burnt alive?
			L_Shown_Norse_Village_Killing_Lepers = L_TRUE
		end if

		until L_Shown_Norse_Village_Killing_Lepers == L_TRUE and L_Shown_Creature_Killing_Lepers == L_TRUE

	end loop

end script L_Spirits_Show_Leper_Deaths



//-----------------------------------------------------------------------------
//	L_Move_Lepers_Via_Waypoints
//-----------------------------------------------------------------------------
begin script L_Move_Lepers_Via_Waypoints
	
start

	L_Waypoints[0]	= marker at {995.713,2.722,1452.279}
	L_Waypoints[1]	= marker at {1029.232,3.015,1432.198}
	L_Waypoints[2]	= marker at {1064.065,3.350,1364.334}
	L_Waypoints[3]	= marker at {1028.068,3.145,1331.552}

	begin loop
		wait until L_Leper_Picked_Up == L_FALSE
		move L_Leper_Flock position to {L_Waypoints[L_Counter_M]}
		wait until {L_Leper_Flock} near {L_Waypoints[L_Counter_M]} radius L_WAYPOINT_RADIUS or L_Leper_Picked_Up == L_TRUE

		if L_Leper_Picked_Up == L_TRUE
			wait until L_Leper_Picked_Up == L_FALSE
			L_Counter_M--
		end if
		L_Counter_M++
		until L_Counter_M == 4
	end loop
	//end of loop

end script L_Move_Lepers_Via_Waypoints



//-----------------------------------------------------------------------------
//	L_Check_If_Leper_Picked_Up
//-----------------------------------------------------------------------------
begin script L_Check_If_Leper_Picked_Up

start
	
	begin loop
		
		while L_Counter_L < L_LEPER_GROUP_SIZE and L_Leper_Picked_Up == L_FALSE
			if L_Leper[L_Counter_L] is HELD
				L_Leper_Picked_Up = L_TRUE
			else
				L_Leper_Picked_Up = L_FALSE
			end if
			L_Counter_L++
			if L_Counter_L > 14
				L_Counter_L = 0
			end if
		end while

		wait until L_Leper_Picked_Up == L_FALSE
		//wait until L_Leper[L_Counter_L] is not HELD
		//L_Leper_Picked_Up = L_FALSE	

		//until L_Counter_L == 100 //... TEMP - not needed

	end loop

end script L_Check_If_Leper_Picked_Up



//-----------------------------------------------------------------------------
//	L_Lepers_Watch_The_Hand
//-----------------------------------------------------------------------------
begin script L_Lepers_Watch_The_Hand

	L_Speech_Variable		= 0

start

	begin loop

		wait until L_Leper_Picked_Up == L_TRUE

		move L_Leper_Flock position to {L_Leper_Flock}

		L_Speech_Variable++

		wait 0.3 seconds

		begin dialogue
			if L_Speech_Variable == 1
				say "Woah! Hey, please don't hurt us! We mean you no harm."
			elsif L_Speech_Variable == 2
				say "Argh! Stop picking us up please!"
			elsif L_Speech_Variable == 3
				say "This picking people up has become a bit of a bad habit for you hasn't it."
			elsif L_Speech_Variable > 3
				say "Please stop grabbing us!"
			end if
			wait until read
			clear dialogue
		end dialogue

		//wait until L_Leper[L_Counter_L] is not HELD //... DOESN'T WORK - why? I don't rightly know :)
		begin loop
			L_Held_Object = get object held
			until L_Held_Object == 0
		end loop
		
		L_Leper_Picked_Up = L_FALSE	

		wait 0.3 seconds

	end loop

end script L_Lepers_Watch_The_Hand



//-----------------------------------------------------------------------------
//	L_Check_For_Any_Dead_Lepers
//-----------------------------------------------------------------------------
begin script L_Check_For_Any_Dead_Lepers

	L_Not_Accepted_Waypoint_1	= marker at {1202.335,3.350,1435.501}
	L_Not_Accepted_Dest			= marker at {1359.728,28.792,1467.305}
	L_Lepers_Current_Position	= 0
	L_Counter_N					= 0

start

	wait until L_Dead_Leper_Counter > 0
	
		stop script "L_Are_Lepers_Within_Radius_1"
		stop script "L_Are_Lepers_Within_Radius_2"
		stop script "L_Check_If_Leper_Picked_Up"
		stop script "L_Are_The_Lepers_Being_Hurt"
		stop script "L_Lepers_Watch_The_Hand"
		stop script "L_Lepers_Accepted_In"
		stop script "L_Move_Lepers_Via_Waypoints"

		//... The lepers are fleeing, so set Accepted to False.
		L_Lepers_Accepted = L_FALSE

		//... Get the current leader's position.  WILL NEED TO USE A MORE RELIABLE POSITION.	
		L_Lepers_Current_Position = marker at {L_Leper_Leader} 
		

		L_Fleeing_Lepers_Group_A = flock at {L_Lepers_Current_Position}
		L_Fleeing_Lepers_Group_B = flock at {L_Lepers_Current_Position}
		set L_Fleeing_Lepers_Group_A properties inner 5 outer 20
		set L_Fleeing_Lepers_Group_B properties inner 5 outer 20
	

		// Split the remaining lepers into 2 groups
		while L_Counter_N < L_LEPER_GROUP_SIZE
			if L_Counter_N < L_LEPER_GROUP_SIZE / 2
				detach L_Leper[L_Counter_N] from L_Leper_Flock
				attach L_Leper[L_Counter_N] to L_Fleeing_Lepers_Group_A
			else
				detach L_Leper[L_Counter_N] from L_Leper_Flock
				attach L_Leper[L_Counter_N] to L_Fleeing_Lepers_Group_B
			end if
			L_Counter_N++
		end while

		wait 2 seconds

		state L_Fleeing_Lepers_Group_A LIVING_MOVE_IN_FLOCK
		state L_Fleeing_Lepers_Group_B LIVING_MOVE_IN_FLOCK

		//... Set the Lepers to Flee away at a high speed
		SPEED of L_Fleeing_Lepers_Group_A = 1.05
		SPEED of L_Fleeing_Lepers_Group_B = 1.05

		//... Move the two groups; one to the Norse creature; the other to the Norse town.
		move L_Fleeing_Lepers_Group_A position to {L_Norse_Creature_Position}
		move L_Fleeing_Lepers_Group_B position to {L_Norse_Town_Outskirts}


		// Start to flee in groups (in terror)	
		begin dialogue
			say "Arghhh! He's killing us! Run everyone!!"
			wait until read
			clear dialogue
		end dialogue


		//... These aren't required.
		wait until {L_Fleeing_Lepers_Group_A} near {L_Norse_Creature_Position} radius L_WAYPOINT_RADIUS
		wait until {L_Fleeing_Lepers_Group_B} near {L_Norse_Town_Outskirts} radius L_WAYPOINT_RADIUS


end script L_Check_For_Any_Dead_Lepers





//====================================================================================================
//====================================== End Of File =================================================
//====================================================================================================



