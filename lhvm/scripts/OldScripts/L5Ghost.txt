
global GhostSamuraiStage = 0
global GSamReleased = 0
global GSam = 0
global GWom = 0
global GMar = 0
global GSamArrived = 0
global GMarArrived = 0
global GReset = 0

define script HandleGSam
define script HandleGWom
define script HandleGMar

//--------------------------------------
// MAIN
//--------------------------------------
begin script L5Ghost

 GSamStartPos = marker at {1425.551,3.648,1644.293}
 GWomStartPos = marker at {1369.631,7.680,1666.862}
 GMarStartPos = marker at {1285.290,6.070,1641.443}

start

	begin loop
		// wait until correct time of night
		wait until get game time > 21 and get game time < 22
		wait 10 seconds
		GSamArrived = 0
		GReset = 0

		// create ghosts
		GSam = create VILLAGER MALE at {GSamStartPos}
		/// ghost GSam
		GWom = create VILLAGER FEMALE at {GWomStartPos}
		/// ghost GWom
		GMar = create VILLAGER MALE at {GMarStartPos}
		/// ghost GMar

		run background script HandleGSam // handles Samurai
		run background script HandleGWom (GWomStartPos) // handles Woman until the final scene
		run background script HandleGMar // handles marauder

		while GReset == 0 and GSamReleased == 0  // wait until final scene resolved
		end while

	until GSamReleased == 1
	end loop

	delete GSam with fade /// preferably do an ascending to heaven effect
	delete GWom with fade
	delete GMar with fade

end script L5Ghost

//-----------------------------------------
// HANDLE GHOST SAMURAI
//-----------------------------------------

begin script HandleGSam

 GSamRoutePos[4]
 Flowers = 0
 FlowerPos = marker at {1416.212,6.028,1679.897}
 FlowerPickingPos = marker at {1415.212,6.028,1678.897}

start
	/// BODGE FLOWERS
	Flowers = create OBJECT ROCK at {FlowerPos}
	SCALE of Flowers = 0.3

	GSamRoutePos[0] = marker at {1413.097,4.690,1648.761}
	GSamRoutePos[1] = marker at {1413.712,6.028,1677.397}
	GSamRoutePos[2] = marker at {1381.120,6.960,1677.548}
	GSamRoutePos[3] = marker at {1376.679,6.921,1671.629}

	move GSam position to {GSamRoutePos[0]}
	wait until GSam near {GSamRoutePos[0]} radius 1
	move GSam position to {GSamRoutePos[1]}
	wait until GSam near {GSamRoutePos[1]} radius 1

	/// ADD DISTRACTION OBJECT AND ANIMS
	/// gather a bunch of flowers
	/// IF ALTERED THEN MOVE STRAIGHT ON TO NEXT POS

	/// TEMP BODGE !
	if Flowers near {FlowerPos} radius 5
		move GSam position to {FlowerPickingPos}
		wait until GSam at {FlowerPickingPos}
		play anim "M_P_pick_up_bag" on GSam loop 3
		wait until GSam played
		///make flower bunch appear in hand?
	end if

	move GSam position to {GSamRoutePos[2]}
	wait until GSam near {GSamRoutePos[2]} radius 1
	move GSam position to {GSamRoutePos[3]}
	wait until GSam near {GSamRoutePos[3]} radius 1

	GSamArrived = 1
	if HEALTH of GWom > 0
		///if GWom not frozen or invisible etc
			///move over and hug her BODGE BELOW
			move GWom position to {GWom}
			move GSam position to {GWom}+{0,1}
			wait until GSam at {GWom}+{0,1}
			set GWom focus to {GSam}
			set GSam focus to {GWom}
			play anim "M_p_couple_kiss_man" on GSam loop 1
			play anim "M_p_couple_kiss_woman" on GWom loop 1
			wait until GSam played and GWom played

			if GMar exists and HEALTH of GMar > 0
				// keep hugging her if the Marauder is yet to arrive until he shows up
				while GMarArrived == 0
					///hug GWom BODGE BELOW
					play anim "M_p_couple_kiss_man" on GSam loop 1
					play anim "M_p_couple_kiss_woman" on GWom loop 1
					wait until GSam played and GWom played
				end while
				/// attack GMar
				/// run over and hug again BODGE BELOW
				move GWom position to {GWom}
				move GSam position to {GWom}+{0,1}
				wait until GSam at {GWom}+{0,1}
				set GWom focus to {GSam}
				set GSam focus to {GWom}
				play anim "M_p_couple_kiss_man" on GSam loop 1
				play anim "M_p_couple_kiss_woman" on GWom loop 1
				wait until GSam played and GWom played
			end if
		///end if
		/// CHA-CHING reward sounds
		GSamReleased = 1
	else
		// move over, mourn her and then commit suicide
		move GSam position to {GWom}
		wait until {GSam} near {GWom} radius 2
		move GSam position to {GSam}
		set GSam focus to {GWom}
		play anim "M_P_Mourning" on GSam loop 2
		wait until GSam played
		/// commit suicide
		delete GSam with fade
		delete GWom with fade
		GReset = 1
	end if

end script HandleGSam

//-----------------------------------------
// HANDLE GHOST WOMAN
//-----------------------------------------

begin script HandleGWom (GWomStartPos)

start

while GSamArrived == 0 and GMarArrived == 0
	// loitering anims
	RndLoc = marker at {GWomStartPos}+{number from -5 to 5, number from -5 to 5}
	move GWom position to {GWomStartPos}
	wait until GWom near {GWomStartPos} radius 1
	wait 1 second
	play anim "M_P_looking_for_something" on GWom loop 1
	wait until GWom played
end while

end script HandleGWom

//-----------------------------------------
// HANDLE GHOST MARAUDER
//-----------------------------------------

begin script HandleGMar

GMarRoutePos[4]
Count = 0

start

	GMarRoutePos[0] = marker at {1301.925,6.333,1648.337}
	GMarRoutePos[1] = marker at {1319.620,5.564,1637.664}
	GMarRoutePos[2] = marker at {1349.821,5.115,1641.156}
	GMarRoutePos[3] = marker at {1364.703,7.792,1661.246}

	// move through waypoints
	while Count < 4
		move GMar position to {GMarRoutePos[Count]}
		wait until GMar near {GMarRoutePos[Count]} radius 1
		Count++
	end while

	while GWom invisible
		play anim "M_P_looking_for_something" on Gmar loop 1
		wait until GMar played
	end while

	if GSamArrived == 0
		/// attack GWom
		wait until HEALTH of GWom <= 0
		// celebration anim
		play anim "M_P_rallyingcrowd" on GMar loop 1
		wait until GMar played
		delete GMar with fade
	else
		/// attack GSam (die trying)
	end if

end script HandleGMar

